import org.lesscss.LessCompiler
import java.time.LocalDate
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.lesscss:lesscss:1.7.0.1.1"
    }
}

plugins {
    id "org.springframework.boot" version "1.5.8.RELEASE"
    id "org.hidetake.ssh" version "2.9.0"
    id "com.eriwen.gradle.css" version "2.14.0"
}

apply plugin: "java"
apply plugin: "org.springframework.boot"

sourceCompatibility = 1.8
def webAppDir = "src/main/webapp"
def resourcesDir = "$webAppDir/resources"

springBoot {
    executable = true
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.projectlombok:lombok:1.16.18"

    compile "org.apache.myfaces.core:myfaces-impl:2.2.12"
    compile "org.apache.myfaces.core:myfaces-api:2.2.12"
    compile "org.apache.tomcat.embed:tomcat-embed-jasper:8.5.16"

    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework.boot:spring-boot-starter-websocket"
    compile "org.springframework.boot:spring-boot-starter-mail"

    compile "com.github.ben-manes.caffeine:caffeine"
    compile "org.hibernate:hibernate-ehcache"

    runtime "mysql:mysql-connector-java"

    compile "org.joinfaces:jsf-myfaces-spring-boot-starter:2.4.1"
    compile "org.primefaces:primefaces:6.1"
    compile "br.com.litecode:litefaces-enum-i18n:1.0.2"
    compile "com.github.adminfaces:admin-theme:1.0.0-RC11"
    compile "org.omnifaces:omnifaces:1.14.1"

    compile "com.google.guava:guava:23.2-jre"
    compile "org.apache.shiro:shiro-core:1.4.0"
    compile "org.apache.shiro:shiro-web:1.4.0"
    compile "org.apache.shiro:shiro-spring:1.4.0"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "com.itextpdf:itextpdf:5.5.12"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.3"


    testCompile "org.springframework.boot:spring-boot-starter-test"
//    testCompile "com.google.guava:guava-testlib:23.2-jre"
    testCompile "org.mockito:mockito-core:2.9.0"
    testCompile "org.powermock:powermock-module-junit4:1.7.1"
    testCompile "org.powermock:powermock-api-mockito2:1.7.1"
    testCompile "org.hsqldb:hsqldb"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:3.6.0"
    testCompile "org.seleniumhq.selenium:selenium-api:3.6.0"
    testCompile "org.seleniumhq.selenium:selenium-support:3.6.0"
    testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.6.0"
    testCompile "org.fluentlenium:fluentlenium-core:3.4.1"
    testCompile "org.fluentlenium:fluentlenium-junit:3.4.1"
    testCompile "org.fluentlenium:fluentlenium-assertj:3.4.1"
}

minifyCss {
    source = "$resourcesDir/css/style.css"
    dest = "$resourcesDir/css/style.min.css"
}

task compileLess {
    doLast {
        def lessCompiler = new LessCompiler()

        ant.replace(file: "$resourcesDir/less/style.less", token: "?ln=less", value: "")
        lessCompiler.compile(file("$projectDir/$resourcesDir/less/style.less"), file("$projectDir/$resourcesDir/css/style.css"))
        ant.replace(file: "$resourcesDir/less/style.less", token: ".less", value: ".less?ln=less")
    }
}

ext.loadVersionProperties = {
    def versionFile = "$projectDir/src/main/resources/version.properties"
    def versionProperties = new Properties()
    file(versionFile).withInputStream { versionProperties.load(it) }
    return versionProperties
}

def currentVersion = loadVersionProperties().version

task updateVersion {
    doLast {
        println(currentVersion)
        if (gradle.startParameter.taskNames[0] != "deploy") {
            return
        }

        def versionProperties = loadVersionProperties()
        def version = versionProperties.version.split("\\.")
        def nextMinorVersion = Integer.parseInt(version[2]) + 1
        def newVersion = version[0] + "." + version[1] + "." + nextMinorVersion

        LocalDate versionDate = LocalDate.parse(versionProperties.versionDate, DateTimeFormatter.ofPattern("dd/MM/yyyy"))

        if (versionDate == LocalDate.now()) {
            println("Version already incremented. Current version: $versionProperties.version")
            return
        }

        def versionFile = "$projectDir/src/main/resources/version.properties"
        ant.propertyfile(file: versionFile) {
            entry(key: "version", value: newVersion)
            entry(key: "versionDate", value: new Date().format('dd/MM/yyyy'))
        }

        println("New version: $newVersion")
    }
}

remotes {
    remoteServer {
        host = project.hasProperty("serverHost") ? serverHost : ""
        user = project.hasProperty("serverUser") ? serverUser : ""
        password = project.hasProperty("serverPassword") ? serverPassword : ""
        knownHosts = allowAnyHosts
    }
}

task deleteCss {
    doLast {
        def cssFiles = [ new File("$webAppDir/resources/css/style.css"), new File("$webAppDir/resources/css/style.min.css") ]
        cssFiles.each { file ->  file.delete() }
    }
}

task deploy(dependsOn: "bootRepackage") {
    doLast {
        ssh.run {
            session(remotes.remoteServer) {
                println("Backing up previous release...")
                def today = new Date().format('yyyy.MM.dd.HH.mm')

                try {
                    execute "cp /opt/hcm/hcm.jar /opt/hcm/releases/hcm-${currentVersion}-${today}.jar"
                } catch (Exception e) {
                    println "Unable to backup previous release"
                }

                println("Uploading JAR...")
                put from: "build/libs/hcm.jar", into: "/opt/hcm/hcm.jar"

                println("Stopping application...")
                execute "systemctl daemon-reload"
                execute "service hcm stop"

                println("Initiating application...")
                execute "chmod +x /opt/hcm/hcm.jar"
                execute "/opt/hcm/hcmStart.sh"
            }
        }
    }
}

compileJava {
    options.compilerArgs = [
        "-parameters"
    ]
}

processResources.dependsOn "updateVersion"
jar.dependsOn = [ "compileLess", "minifyCss" ]

jar.into('META-INF/resources') {
    from('src/main/webapp')
}

jar.finalizedBy deleteCss
jar.baseName = "hcm"

test {
    exclude "br/com/litecode/integration/**"
}